// so that the bootspork is very useful in pvp without being over powered
// in single player / co-op
Actor PvPSporkPuff : ZorchPuff
{
	Damagetype "PvPSporkPuff"
}

// these actors stay, and never give dual bootsporks. 

actor BootSpork : SuperBootspork 11910
{
	//$Category zorchers
	//$Sprite CSAWA0
	//$Title "Bootspork -- Stays"
	Weapon.Slotnumber 1
	+WEAPON.CHEATNOTWEAPON
	+WEAPON.NOLMS
	+WEAPON.NOALERT

	Inventory.PickupMessage "You got the \cjsuper bootspork!"
	Inventory.Icon "CSAWA0"
	
	States
	{
	Fire:
    SAWG AB 4 A_Saw("weapons/sawfull","weapons/sawhit", 0 , "PvPSporkPuff")
    SAWG B 0 A_ReFire
    Goto Ready
	}
	
}	
		
// do not use this as a pickup
actor DualBootsporkTEMP : SuperBootspork
{
  Obituary "%o had no defense against %k's dual bootspork rush"
  Inventory.Pickupmessage "This is not meant to be a pickup, use Dualsporkgiver instead!"
  Weapon.Selectionorder 1700
  Inventory.Icon "DSAWA0"
  +WEAPON.NOALERT

  Weapon.Slotnumber 1
	
  States
  {
  Ready:
    DAWG A 4  A_WeaponReady
	DAWG A 0 A_Playsound("Weapons/sawidle")
	DAWG B 4 A_WeaponReady
    loop
  Deselect:
    DAWG A 1 A_Lower
    loop
  Select:
    DAWG A 1 A_Raise
    loop
  Fire:
    DAWG CCDD 2 A_Saw("weapons/sawfull","weapons/sawhit", 0 , "PvPSporkPuff")
    DAWG C 0 A_ReFire
    goto Ready
  Spawn:
    TLGL E -1
    stop
  }
}

// behold! The giver of the spork! Use this as a pickup
actor SporkGiver : Custominventory replaces SuperBootSpork 2005
{
	//$Category zorchers
	//$Sprite CSAWA0
	//$Title "Bootspork -- Dual Wieldable"
	Inventory.Pickupsound "misc/w_pkup"
	Inventory.Pickupmessage "Picked up a \cjsuper bootspork! \cvPick up two to dual wield!"
	states
	{
	Spawn:
		CSAW A -1 
		stop
	Pickup:
		TNT1 A 0 A_JumpifInventory("DualBootsporkTEMP",1,3)
		TNT1 A 0 A_JumpifInventory("Bootspork",1,"Give1")
		TNT1 A 0 A_GiveInventory("Bootspork",1)
		stop
		
	Give1:
		TNT1 A 0 A_TakeInventory("Bootspork",1)
		TNT1 A 0 A_GiveInventory("DualBootsporkTEMP",1)
		stop
	}
}


// use this as a pickup
actor DualBootSporks : Custominventory 11010
{
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Picked up \cjtwo bootsporks\cg! time for some serious kitchen utensil action!"
	states
	{
		Spawn:
			DSAW A -1
			stop
		Pickup:
			DSAW A 0 A_JumpIfInventory("DualBootSporkTEMP",1,"GOTIT")
			TNT1 A 0 A_TakeInventory("Bootspork")
			TNT1 A 0 A_GiveInventory("DualBootSporkTEMP")
			TNT1 A 0 A_SpawnItem("DualBootSporks")
			stop
		GOTIT:
			DSAW A 0
			fail
	}
}

Actor DropDualBootSporks : DualBootSporks
{
	States
	{
		Pickup:
		TNT1 A 0 A_TakeInventory("Bootspork")
		TNT1 A 0 A_GiveInventory("DualBootSporkTEMP")
		stop
	}
}
		
