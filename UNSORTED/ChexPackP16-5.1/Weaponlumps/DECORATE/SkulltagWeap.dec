//////////////////////////////////////////////////////////////////////
//Weapons start at 11000
////////////////////////////////////////////////////////////////////
    
Actor Ultrarapidzorcher : Weapon  11000
{
	Weapon.Slotnumber 4
	Weapon.SelectionOrder 500
	Weapon.AmmoType "ZorchClip"
	Weapon.AmmoGive 30
	SpawnID 214
	Radius 20
	Height 16
	Weapon.AmmoUse 1
	Weapon.Kickback 100
  
	Game Chex
	
	AttackSound "weapons/chngun"
	Obituary "%o got a zorch rash from %k's Ultra Rapid Zorcher"
	Inventory.PickupMessage "You stumbled upon an \cjUltra rapid zorcher!"
  
	Inventory.Icon "MNGNA0"
	
	-NOGRAVITY
	
  States
  {
	  Spawn:
		MNGN A -1
		Loop
	  Ready:
		MNGG A 1 A_WeaponReady
		Loop
	  Deselect:
		MNGG A 1 A_Lower
		Loop
	  Select: 
		MNGG A 1 A_Raise 
		Loop  
	  Fire: 
		MNGG A 0 A_GunFlash
		MNGG A 2 A_FireBullets(5.6, 0, 1, 5, "BulletPuff")
		MNGG B 2 A_FireBullets(5.6, 0, 1, 5, "BulletPuff")
		MNGG A 2 A_ReFire
		MNGG B 2
		MNGG A 4
		MNGG B 4
		MNGG A 8
		MNGG B 8
		Goto Ready 
	  Flash: 
		MNGF A 3 BRIGHT A_Light1 
		MNGF B 3 BRIGHT A_Light0
		Stop 
	  }
}

//++++++++++++++++++++++++++++++++++++++++++++++++++++
//
//        Zorchlauncher (Grenade Launcher)
//
//+++++++++++++++++++++++++++++++++++++++++++++++++++

ACTOR ZorchLauncher : Weapon 11001
{
	SpawnID 163
	Radius 20
	Height 16
	Weapon.Selectionorder 2500
	Weapon.AmmoType "ZorchRocketAmmo"
	Game Chex
	Weapon.Slotnumber 5
	Weapon.AmmoGive 2
	Inventory.Pickupmessage "You just found a \cj Zorch Launcher!"
	Inventory.Icon "GLAUA0"
	Weapon.Kickback 100
	Weapon.AmmoUse 1
	
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	
	States
	{
	  Spawn:
		GLAU A -1
		Stop
	  Ready:
		GRLG A 1 A_WeaponReady
		Loop
	  Deselect:
		GRLG A 1 A_Lower
		Loop
	  Select: 
		GRLG A 1 A_Raise
		Loop
	  Fire: 
		GRLG B 8 A_GunFlash
		GRLG B 12 A_FireCustomMissile ("ZorchGrenade", 0, true, 0, 0, 0, 6.328125)
		GRLG B 0 A_ReFire
		Goto Ready 
	  Flash: 
		GRLF A 3 bright A_Light1
		GRLF B 4 bright
		GRLF C 4 bright A_Light2
		GRLF D 4 bright A_Light2
		Goto LightDone
	}
}

ACTOR ZorchGrenade
{
  
	  SpawnID 216
	  Radius 8
	  Height 8
	  Speed 25
	  Damage 20
	  Projectile
	  -NOGRAVITY
	  +RANDOMIZE
	  +DEHEXPLOSION
	  +GRENADETRAIL
	  BounceType "Doom"
	  Gravity 0.25
	  SeeSound "weapons/grenlf"
	  DeathSound "weapons/grenlx"
	  //BounceSound "weapons/grbnce"
	  Game Chex
	  Obituary "%o tripped over %k's zorch grenade"
	  DamageType Grenade
	  States
	  {
	  Spawn:
		SGRN A 1 Bright 
		Loop
	  Death:
		MISL B 8 Bright A_Explode
		MISL C 6 Bright
		MISL D 4 Bright
		Stop
	  Grenade:
		MISL A 1000 A_Die
		Wait
	  Detonate:
		MISL B 4 A_Scream
		MISL C 6 A_Detonate
		MISL D 10
		Stop
	  Mushroom:
		MISL B 8 A_Mushroom
		Goto Death+1
	  }
}

//=================================================================================================
//
// Rail Zorcher
//
//=================================================================================================

// the attack looks like it is coming from chexter's head sometimes, I don't know why. 

//orig code by 75, with some edits by combinebobnt

ACTOR Railgunammocounter : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 4
}

ACTOR RailZorcher : Weapon 11002
{
  Game chex
  Weapon.Slotnumber 6
  Weapon.Ammotype "ZorchCell"
  Weapon.Ammouse 10
  Weapon.Ammotype2 "Railgunammocounter"
  Weapon.Ammouse2 0
 
  Inventory.Pickupmessage "You now carry a \cj rail zorcher!" 
  Obituary "%o was sniped by %k's rail zorcher" 
  Inventory.Icon "RAILA0"
  
  SpawnID 164
  Radius 20
  Height 16
  Weapon.Selectionorder 100
  Weapon.AmmoGive 40
  Weapon.AmmoGive2 4
  Weapon.SlotNumber 6 
  
  States
	  {  
	  	Spawn:
			RAIL A -1
			Stop
		Fire:
			RLGG E 0 A_Jumpifinventory("Railgunammocounter",1,1)
			Goto Reload
			
			RLGG E 0 A_TakeInventory("Railgunammocounter",1)
			RLGG E 12 A_FireRailgun
			RLGG A 6
			
			RLGG E 0 A_Jumpifinventory("Railgunammocounter",1,1)
			Goto Reload
			
			RLGG M 0 A_ReFire
			Goto Ready
		Altfire:
			RLGG A 0 A_Jumpifinventory("Railgunammocounter",4,1)
			Goto Reload
			RLGG A 0
			Goto AltHold
		AltHold:
			RLGG A 1
			RLGG A 0 A_ReFire
			Goto Ready
		Reload:
			RLGG F 3 A_PlayWeaponSound("railclip")
			RLGG G 3
			RLGG H 11
			RLGG IJ 3
			RLGG A 0 A_GiveInventory("Railgunammocounter",4)
			RLGG K 3 A_PlayWeaponSound("railload")
			RLGG L 7
			RLGG A 6
			RLGG M 0 A_ReFire
			Goto Ready
		Flash:
			TNT1 A 5 bright A_Light1
			TNT1 A 5 bright A_Light2
			TNT1 A 0 bright A_Light0
			Goto LightDone
		Ready:
			RLGG A 1 A_WeaponReady
			Loop
		Deselect:
			RLGG A 1 A_Lower
			Loop
		Select:
			RLGG A 1 A_Raise
			Loop
	   }

}

//RLGG F 6 A_CheckForReload(4, "Reloaded") - jumps to "reloaded" if the function has not been called four times.

//=================================================================================================
//
// Liquid Zorcher
//
//=================================================================================================

ACTOR  LiquidZorcher : weapon 11003
{
Game Chex
  Weapon.Slotnumber 7 
  Weapon.Ammotype "ZorchCell"
  Inventory.Pickupmessage "You are now the master of a \cJliquid \cAzorcher!"
  Inventory.Icon "BFG2A0"
   SpawnID 165
  Radius 20
  Height 20
  Weapon.Selectionorder 2800
  Weapon.AmmoUse 10
  Weapon.AmmoGive 20
  Weapon.AmmoType "ZorchCell"
  Weapon.Kickback 100
  Weapon.SlotNumber 7 
  +WEAPON.NOAUTOFIRE
  +WEAPON.NOLMS
  States
  {
	Spawn:
		BFG2 A -1
		Stop
	Ready:
		BG2G A 0 A_PlaySound ("weapons/liquididle")
		BG2G A 1 A_WeaponReady
		BG2G A 1 A_WeaponReady
		BG2G A 1 A_WeaponReady
		BG2G B 1 A_WeaponReady
		BG2G B 1 A_WeaponReady
		BG2G B 1 A_WeaponReady
		BG2G C 1 A_WeaponReady
		BG2G C 1 A_WeaponReady
		BG2G C 1 A_WeaponReady
		BG2G D 1 A_WeaponReady
		BG2G D 1 A_WeaponReady
		BG2G D 1 A_WeaponReady
		Loop
	Fire:
		BG2G E 20 A_PlaySound ("weapons/liqfir")
		BG2G F 4
		BG2G G 1
		BG2G H 1
		BG2G I 1
		BG2G J 1
		Goto Hold
	Hold:
		BG2G K 2 A_GunFlash
		BG2G L 2 A_FireBullets(0,0,1,0,"LiquidZorcherShot")
		BG2G M 2
		BG2G N 0 A_ReFire
		BG2G O 35 A_PlaySound ("weapons/liqdown")
		Goto Ready
	Flash:
		TNT1 A 2 Bright A_Light1
		TNT1 A 3 Bright
		Goto LightDone
		Stop
	Deselect:
		BG2G E 1 A_Lower
		Loop
	Select:
		BG2G E 1 A_Raise
		Loop
  }
}

ACTOR LiquidZorcherShot
{
	SpawnID 217
	Radius 11
	Height 8
	Speed 20
	Damage 160
	DamageType BFG10k
	+NOBLOCKMAP
	+NOGRAVITY
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+NOTELEPORT 
	+PUFFONACTORS	
	+PUFFGETSOWNER
	+FORCERADIUSDMG
	Renderstyle Add
	Alpha 0.75
	SeeSound "weapons/bfgx"
	AttackSound "weapons/bfgx"
	Game Chex
	Obituary "%o never stood a chance against %k's liquid zorcher"
	States
	{
	Spawn:
		BFE1 A 0 Bright
		BFE1 A 3 Bright A_Detonate
		BFE1 BCDEF 3 Bright
		Stop
	}
}
