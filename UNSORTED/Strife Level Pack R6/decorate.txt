// DECORATE lump for The Ultimate Chex Quest

// ##########################################################################
// #                                                                        #
// #                               Player Pawn                              #
// #                                                                        #
// ##########################################################################

ACTOR ChexPlayer : PlayerPawn
{
  Damagefactor "Flemoid", 1.0 
  BloodColor 111 239 103
  Speed 1
  Health 100
  Radius 16
  Height 56
  Mass 100
  PainChance 255
  +FORCEXYBILLBOARD
  Player.DisplayName "Chex Warrior"
  Player.DamageScreenColor "60 b0 58"
  Player.StartItem "MiniZorcher"
  Player.StartItem "Bootspoon"
  Player.StartItem "Clip", 50
  Player.WeaponSlot 1, Bootspoon, Bootspork
  Player.WeaponSlot 2, MiniZorcher
  Player.WeaponSlot 3, LargeZorcher, DoubleLargeZorchers
  Player.WeaponSlot 4, RapidZorcher
  Player.WeaponSlot 5, Propulsor
  Player.WeaponSlot 6, PhasingZorcher
  Player.WeaponSlot 7, LAZDevice
  Player.ColorRange 192, 207 
//  Player.ColorSet 0, "Light Blue",    0xC0, 0xCF,  0xC2
//  Player.ColorSet 1, "Green",         0x70, 0x7F,  0x72
//  Player.ColorSet 2, "Gray",          0x60, 0x6F,  0x62
//  Player.ColorSet 3, "Brown",         0x40, 0x4F,  0x42
//  Player.ColorSet 4, "Red",           0x20, 0x2F,  0x22
//  Player.ColorSet 5, "Light Gray",    0x58, 0x67,  0x5A
//  Player.ColorSet 6, "Light Brown",   0x38, 0x47,  0x3A
//  Player.ColorSet 7, "Light Red",     0xB0, 0xBF,  0xB2

  States
  {
  Spawn:
    PLAY A -1
    Loop
  See:
    PLAY ABCD 4 
    Loop
  Missile:
    PLAY E 12 
    Goto Spawn
  Melee:
    PLAY F 6 BRIGHT
    Goto Missile
  Pain:
    PLAY G 4 
    PLAY G 4 A_Pain
    Goto Spawn
  Death:
    PLAY H 0 A_PlayerSkinCheck("AltSkinDeath")
  Death1:
    PLAY H 10
    PLAY I 10 A_PlayerScream
    PLAY J 10 A_NoBlocking
    PLAY K -1
    Stop
  XDeath:
    PLAY O 0 A_PlayerSkinCheck("AltSkinXDeath")
  XDeath1:
    PLAY O 5
    PLAY P 5 A_XScream
    PLAY Q 5 A_NoBlocking
    PLAY RSTUV 5
    PLAY W -1
    Stop
  }
}

ACTOR DoubleLargeZorchers : SuperShotgun 82
{
  SpawnID 33
  Weapon.SelectionOrder 400
  Weapon.AmmoUse 2
  Weapon.AmmoGive 8
  Weapon.AmmoType "Shell"
  Obituary "%o was hit by %k's double zorchers."
  Inventory.PickupMessage "You got double large zorchers!"
  States
  {
  Ready:
    SHT2 A 1 A_WeaponReady
    Loop
  Deselect:
    SHT2 A 1 A_Lower
    Loop
  Select:
    SHT2 A 1 A_Raise
    Loop
  Fire:
    SHT2 A 3
    SHT2 A 7 A_FireShotgun2
    SHT2 B 7
    SHT2 C 7 A_CheckReload
    SHT2 D 7 A_Light0
    SHT2 E 7
    SHT2 F 7 A_Light0
    SHT2 G 6
    SHT2 H 6 A_CloseShotgun2
    SHT2 A 5 A_ReFire
    Goto Ready
    // unused states
    SHT2 B 7
    SHT2 A 3
    Goto Deselect
  Flash:
    SHT2 I 4 Bright A_Light1
    SHT2 J 3 Bright A_Light2
    Goto LightDone
  Spawn:
    SGN2 A -1
    Stop
  }
}

ACTOR Arachnus replaces Arachnotron
{
	DamageFactor "Flemoid" , 0
	BloodColor 111 239 103
	Health 20
	Radius 20
	Height 30
	Mass 80
	Speed 12
	ReactionTime 8
	PainChance 256
	Monster
	+SOLID
	+SHOOTABLE
	+DROPOFF
	+COUNTKILL
	+SKULLFLY
	+NEVERRESPAWN
	SeeSound "arachnus/sight"
	PainSound "arachnus/sight"
	DeathSound "weapons/rocklf"
	ActiveSound "arachnus/active"
	Obituary "%s got a face full of Arachnus slime"
	States
	{
	Spawn:
		ARAC A 10 A_Look
		Loop
	See:
		ARAC A 2 A_BabyMetal
		ARAC BCDAB 2 A_Chase
		ARAC C 2 A_BabyMetal
		ARAC DABCD 2 A_Chase
		Goto See+1
	Melee:
		TNT1 A 0
		Goto Death
	Pain:
		ARAC E 4
		ARAC E 4 A_Pain
		Goto See+1
	Death:
		ARAC F 4 A_SargAttack
		ARAC G 1 A_NoBlocking
		ARAC H 5
		ARAC I 5
		ARAC J 5
		TNT1 A -1
		Stop
	}
}

ACTOR Tenacious : Cacodemon replaces Cacodemon
{
	DamageFactor "Flemoid" , 0
	BloodColor 111 239 103
	Radius 35
	Mass 500
	Speed 10
	SeeSound "demon/sight"
	DeathSound "baron/death"
	Obituary "%s was slimed by a Tenacious"
	States
	{
	Spawn:
		HEAD C 10 A_Look
		Loop
	See:
		HEAD A 3 A_Chase
		HEAD B 1 A_Chase
		Loop
	Missile:
		HEAD D 5 A_FaceTarget
		HEAD E 5 BRIGHT A_HeadAttack
		HEAD F 5 A_FaceTarget
		Goto See
	Pain:
		HEAD G 3
		HEAD G 3 A_Pain
		HEAD G 6
		Goto See
	Death:
		HEAD H 8 A_Scream
		HEAD I 8
		HEAD J 8
		HEAD K 8 A_NoBlocking
		HEAD L 8 A_SetFloorClip
		TNT1 A -1
		Stop
	Raise:
		HEAD L 8 A_UnSetFloorClip
		HEAD KJIHG 8
		Goto See
	}
}

ACTOR TenaciousBall : CacodemonBall replaces CacodemonBall
{
	Speed 20
	FastSpeed 40
	Damage 4
	States
	{
	Spawn:
		BAL1 AB 4 BRIGHT
		Loop
	Death:
		BAL1 CDE 6 BRIGHT
		Stop
	}
}

ACTOR FrostedChexArmor : Megasphere replaces Megasphere
{
	Inventory.PickupMessage "Got the frosted Chex(r) Armour!"
}	

ACTOR ExplosiveCanister : ExplosiveBarrel replaces ExplosiveBarrel
{
	DeathSound "weapons/rocklf"
}