// POWERUP POD STRING FOUNTAINS ////////
// from STRONGHOLD 		////////
// by Tormentor667 		////////
////////////////////////////////////////

Actor BaseLine
{
 +Missile
 +NoGravity
 +NoBlockmap
 +NoInteraction
 -Solid
 Radius 0
 Height 0
 RenderStyle Add
 Alpha 0.01
 Scale 0.37
 States
 {
  Spawn:
   SPFX AAAAA 1 Bright A_FadeIn(0.2)
   SPFX AAAAAAAAAAAAAAAA 1 Bright A_FadeOut(0.06)
   Stop
 }
}

actor WhitePowerPad 191
{
//$Category Stronghold_PowerUpPads
	+NoBlockMap +NoGravity +NoSector +NoInteraction +NoClip -Solid +CLIENTSIDEONLY
        states
        {
        Spawn:
           Active:
        	TNT1 A 0
        	TNT1 A 0 A_JumpIf(Args[1], "Circle")
        	TNT1 A 1 A_SpawnItemEx ("BaseLine", random(-Args[0], Args[0]), random(-Args[0], Args[0]), random(0, 8), 0, 0, random(1,3), 0, 129, 0) 
        	Loop
           Circle:
        	TNT1 A 0
        	TNT1 A 1 A_SpawnItemEx ("BaseLine", random(-Args[0], Args[0]), 0, random(0, 8), 0, 0, random(1,3), Random(0, 360), 129, 0) 
        	Loop
           Inactive:
		TNT1 A 1
		Loop
        }     
        
}

actor BluePowerPad : WhitePowerPad 192
{
//$Category Stronghold_PowerUpPads
	translation "80:111=196:207"
}

actor GreenPowerPad : WhitePowerPad 193
{
//$Category Stronghold_PowerUpPads
	translation "80:111=112:127"
}

actor RedPowerPad : WhitePowerPad 194
{
//$Category Stronghold_PowerUpPads
	translation "80:111=176:191"
}

actor OrangePowerPad: WhitePowerPad 195
{
//$Category Stronghold_PowerUpPads
	translation "80:111=214:223"
}

//Place the RainDropSpawner on your map, and use values set on the specials tab to modify it

//The first argument controls it's area. 128, for example makes it a 256x256 square or 256-radius circle (See fourth argument)
//The second argument controls it's frequency. The lower the number, the heavier the rainfall
//The third argument controls whether or not it plays it's ambient sound. 0 will play the sound, 1 will not
//The fourth argument controls the area type. 0 is a square, 1 is a circle


Actor RainSpawner 31543
{
  Radius 1
  Height 1
  +NoClip
  +ClientSideOnly
  +SpawnCeiling
  +NoGravity
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_JumpIf(Args[2] > 0, "NoSound")
    TNT1 A 0 A_JumpIf(Args[3] > 0, "Circle")
    TNT1 A 0 A_PlaySoundEx("Ambient/Rain", "SoundSlot7", 1)
    TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), Random(-Args[0], Args[0]), -2, 0, 0, 0, 0, 128, Args[1])
    Loop
  Circle:
    TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), 0, -2, 0, 0, 0, Random(0, 360), 128, Args[1])
  NoSound:
    TNT1 A 0 A_Jumpif(Args[3] > 0, "NoSoundCircle")
    TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), Random(-Args[0], Args[0]), -2, 0, 0, 0, 0, 128, Args[1])
    Loop
  NoSoundCircle:
    TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), 0, -2, 0, 0, 0, Random(0, 360), 128, Args[1])
    Loop
  }
}

Actor RainDrop
{
  +Missile
  +NoBlockMap
  -NoGravity
  Height 2
  Radius 2
  Renderstyle Translucent
  Alpha 0.9
  Scale 1
  States
  {
  Spawn:
    RNDR A 1 A_JumpIf(WaterLevel > 0, "Death")
    Loop
  Death:
    RNDR BCDEFGH 3 A_FadeOut(0.15)
    Stop
  }
}

Actor SteamSpawner : SwitchableDecoration 10000 //DoomEdNum Here
{
  Height 2
  Radius 1
  Mass 0
  +NoBlockMap
  +NoGravity
  +NoInteraction
  +ClientSideOnly
  States
  {
  Spawn:
  Active:
    TNT1 A 0
    TNT1 A 0 A_JumpIf(Args[0] == 1, "Down")
    TNT1 A 0 A_JumpIf(Args[0] > 1, "Forward")
    TNT1 A 0 A_JumpIf(Args[1] > 0, "UpBurst")
    TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
    TNT1 A 0 A_PlaySoundEx("Steam/Loop", "SoundSlot7", 1)
    TNT1 A 1 A_SpawnItemEx("SteamParticle", 0, 0, 1, (0.1)*Random(0, 4), 0, (0.1)*Random(35, 40), Random(0, 360), 128)
    Loop
  Down:
    TNT1 A 0 A_JumpIf(Args[1] > 0, "DownBurst")
    TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
    TNT1 A 0 A_PlaySoundEx("Steam/Loop", "SoundSlot7", 1)
    TNT1 A 1 A_SpawnItemEx("SteamParticle", 0, 0, 0, (0.1)*Random(0, 4), 0, (-0.1)*Random(35, 40), Random(0, 360), 128)
    Loop
  Forward:
    TNT1 A 0 A_JumpIf(Args[1] > 0, "ForwardBurst")
    TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
    TNT1 A 0 A_PlaySoundEx("Steam/Loop", "SoundSlot7", 1)
    TNT1 A 1 A_SpawnItemEx("SteamParticle", 0, 0, 0, (0.1)*Random(35, 40), 0, (0.1)*Random(-5, 5), Random(-8, 8), 128)
    Loop
  UpBurst:
    TNT1 A 1 A_Jump(Args[2]/16, 1)
    Loop
    TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
    TNT1 A 0 A_PlaySoundEx("Steam/Fire", "SoundSlot7", 0)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("SteamParticle", 0, 0, 1, (0.1)*Random(0, 4), 0, (0.1)*Random(35, 40), Random(0, 360), 128)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("SteamParticle", 0, 0, 1, (0.1)*Random(0, 4), 0, (0.1)*Random(35, 40), Random(0, 360), 128)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("SteamParticle", 0, 0, 1, (0.1)*Random(0, 4), 0, (0.1)*Random(35, 40), Random(0, 360), 128)
    Loop
  DownBurst:
    TNT1 A 1 A_Jump(Args[2]/16, 1)
    Loop
    TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
    TNT1 A 0 A_PlaySoundEx("Steam/Fire", "SoundSlot7", 0)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("SteamParticle", 0, 0, 0, (0.1)*Random(0, 4), 0, (-0.1)*Random(35, 40), Random(0, 360), 128)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("SteamParticle", 0, 0, 0, (0.1)*Random(0, 4), 0, (-0.1)*Random(35, 40), Random(0, 360), 128)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("SteamParticle", 0, 0, 0, (0.1)*Random(0, 4), 0, (-0.1)*Random(35, 40), Random(0, 360), 128)
    Loop
  ForwardBurst:
    TNT1 A 1 A_Jump(Args[2]/16, 1)
    Loop
    TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
    TNT1 A 0 A_PlaySoundEx("Steam/Fire", "SoundSlot7", 0)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("SteamParticle", 0, 0, 0, (0.1)*Random(35, 40), 0, (0.1)*Random(-5, 5), Random(-8, 8), 128)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("SteamParticle", 0, 0, 0, (0.1)*Random(35, 40), 0, (0.1)*Random(-5, 5), Random(-8, 8), 128)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("SteamParticle", 0, 0, 0, (0.1)*Random(35, 40), 0, (0.1)*Random(-5, 5), Random(-8, 8), 128)
    Loop
  Inactive:
    TNT1 A 1 A_StopSoundEx("SoundSlot7")
    TNT1 A 1
    Goto Inactive+1
  }
}

Actor SteamParticle
{
  Height 1
  Radius 1
  +Missile
  +NoGravity
  +NoBlockMap
  RenderStyle Add
  Scale 0.4
  Alpha 0.65
  States
  {
  Spawn:
    STEM A 2 A_SetTranslucent(0.6, 1)
    STEM B 2 A_SetTranslucent(0.55, 1)
    STEM C 2 A_SetTranslucent(0.5, 1)
    STEM D 2 A_SetTranslucent(0.45, 1)
    STEM E 2 A_SetTranslucent(0.4, 1)
    STEM F 2 A_SetTranslucent(0.35, 1)
    STEM G 2 A_SetTranslucent(0.3, 1)
    STEM H 2 A_SetTranslucent(0.25, 1)
    STEM I 2 A_SetTranslucent(0.2, 1)
    STEM J 2 A_SetTranslucent(0.15, 1)
    STEM K 2 A_SetTranslucent(0.1, 1)
    STEM L 2 A_SetTranslucent(0.05, 1)
    Stop
  }
}

ACTOR RedGlitterGenerator 16574
{
	+NOBLOCKMAP 
	+NOSECTOR 
	+NOGRAVITY
	+DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 8 A_SpawnItemEx("TeleGlitter1", random[TeleGlitter](0,31)-16, random[TeleGlitter](0,31)-16, 0, 0,0,0.25)
		Loop
	}
}

ACTOR BlueGlitterGenerator 3452
{
	+NOBLOCKMAP 
	+NOSECTOR 
	+NOGRAVITY
	+DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 8 A_SpawnItemEx("TeleGlitter2", random[TeleGlitter2](0,31)-16, random[TeleGlitter2](0,31)-16, 0, 0,0,0.25)
		Loop
	}
}

Actor CloudSpawner 10001
{
  +NoBlockMap
  +NoGravity
  +NoSector
  +NoInteraction
  +NoClip 
  -Solid
  +ClientSideOnly
  Translation "80:111=48:79"
  States
  {
  Spawn:
  Active:
    TNT1 A 0
    TNT1 A 0 A_JumpIf(Args[3] == 1, "Small")
    TNT1 A 0 A_JumpIf(Args[3] == 2, "Medium")
    TNT1 A 0 A_JumpIf(Args[3] > 2, "Large")
    TNT1 A 30 A_SpawnItemEx("MiniCloud", Random(Args[0]*2, -Args[0]*2), Random(Args[0]*2, -Args[0]*2), Random(0, Args[1]), Random(5, 15), 0, 0, Random(6, -6), 129, Args[2])
    Loop
  Small:
    TNT1 A 30 A_SpawnItemEx("SmallCloud", Random(Args[0]*2, -Args[0]*2), Random(Args[0]*2, -Args[0]*2), Random(0, Args[1]), Random(5, 15), 0, 0, Random(6, -6), 129, Args[2])
    Loop
  Medium:
    TNT1 A 30 A_SpawnItemEx("MediumCloud", Random(Args[0]*2, -Args[0]*2), Random(Args[0]*2, -Args[0]*2), Random(0, Args[1]), Random(5, 15), 0, 0, Random(6, -6), 129, Args[2])
    Loop
  Large:
    TNT1 A 30 A_SpawnItemEx("LargeCloud", Random(Args[0]*2, -Args[0]*2), Random(Args[0]*2, -Args[0]*2), Random(0, Args[1]), Random(5, 15), 0, 0, Random(6, -6), 129, Args[2])
    Loop
  Inactive:
    TNT1 A -1
    Loop
  }
}

Actor MiniCloud
{
  +Missile
  +NoBlockmap
  +NoGravity
  Radius 8
  Height 8
  RenderStyle Add
  Alpha 0.1
  Scale 0.5
  States
  {
  Spawn:
    CLFX AAAAA 3 A_FadeIn(0.1)
    CLFX A 1000 Bright 
    Goto Spawn+6
  Death:
    CLFX AAAAAA 10 A_FadeOut(0.1)
    TNT1 A 1
    Stop
  }
}

Actor SmallCloud : MiniCloud
{
  Scale 1.0
  Radius 16
  Height 16
}

Actor MediumCloud : MiniCloud
{
  Scale 2.0
  Radius 32
  Height 32
}

Actor LargeCloud : MiniCloud
{
  Scale 4.0
  Radius 64
  Height 64
}

/////////////////////////////
// SPARKLE FOUNTAINS ////////
// for UTNT          ////////
// by Tormentor667   ////////
/////////////////////////////

Actor BaseStar
{
 +Missile
 +NoGravity
 +NoBlockmap
 +NoInteraction
 -Solid
 Radius 0
 Height 0
 RenderStyle Add
 Alpha 0.01
 Scale 0.20
 States
 {
  Spawn:
   PTCL AABBCCAA 1 Bright A_FadeIn(0.2)
   PTCL AABBCCAABBCCAABBCC 1 Bright A_FadeOut(0.06)
   Stop
 }
}

actor WhiteSparkleSpawner 209
{
	+NoBlockMap +NoGravity +NoSector +NoInteraction +NoClip -Solid +CLIENTSIDEONLY
        states
        {
        Spawn:
           Active:
        	TNT1 A 0
        	TNT1 A 0 A_JumpIf(Args[1], "Circle")
        	TNT1 A 1 A_SpawnItemEx ("BaseStar", random(-Args[0], Args[0]), random(-Args[0], Args[0]), random(0, 8), 0, 0, random(1,4), 0, 129, 0) 
        	Loop
           Circle:
        	TNT1 A 0
        	TNT1 A 1 A_SpawnItemEx ("BaseStar", random(-Args[0], Args[0]), 0, random(0, 8), 0, 0, random(1,4), Random(0, 360), 129, 0) 
        	Loop
           Inactive:
		TNT1 A 1
		Loop
        }     
        
}

actor BlueSparkleSpawner : WhiteSparkleSpawner 210
{
	translation "80:111=196:207"
}

actor GreenSparkleSpawner : WhiteSparkleSpawner 211
{
	translation "80:111=112:127"
}

actor RedSparkleSpawner : WhiteSparkleSpawner 212
{
	translation "80:111=176:191"
}

actor OrangeSparkleSpawner: WhiteSparkleSpawner 213
{
	translation "80:111=214:223"
}

actor PurpleSparkleSpawner: WhiteSparkleSpawner 214
{
	translation "80:111=250:254"
}

//////////////////////
// SNOW SPAWNERS    //
// by Tormentor667  //
//////////////////////

Actor SnowParticle
{
  +MISSILE +NoBlockMap 
  Radius 1
  Height 1
  Damage 0
  Projectile
  RenderStyle Translucent
  Alpha 0.0
  Scale 0.7
  States
  {
  Spawn:
    SNOW AAAAAAA 2 A_FadeIn(0.1)
    SNOW A 3
    goto Spawn+7
  Death:
    SNOW AAAAAA 1 A_FadeOut(0.2)
    stop
  }
}

Actor SnowSpawner 10002
{
 +NoBlockMap +NoGravity +NoSector +NoInteraction +NoClip -Solid +CLIENTSIDEONLY +SPAWNCEILING 
 Radius 1
 Height 1
 States
 {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_JumpIf(Args[2] > 0, "Circle")
    TNT1 A 0 A_CheckSight("Unsighted")
    TNT1 A 8 A_SpawnItemEx ("SnowParticle", Random(-Args[0], -Args[0]), Random(-Args[0], -Args[0]), 0, Random(0, 1), Random(0, 1), Random(-1, -3), 0, 128, Args[1])
    Loop
  Circle:
    TNT1 A 0 A_CheckSight("Unsighted")
    TNT1 A 8 A_SpawnItemEx ("SnowParticle", Random(-Args[0], -Args[0]), 0, 0, Random(0, 1), Random(0, 1), Random(-1, -3), Random(0, 360), 128, Args[1]) 
    Loop
  Unsighted:
    TNT1 A 1
    Goto Spawn
 }
}
