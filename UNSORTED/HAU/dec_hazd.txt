actor FlemLandMine 30248
{
  speed 0
  radius 20
  height 16
  deathsound "imp/shotx"
  minmissilechance 0
  health 10
  maxtargetrange 10
  obituary "%o was carelessly blown away by a landmine."
  MONSTER
  -COUNTKILL
  +NORADIUSDMG
  +LOOKALLAROUND
  +MISSILEMORE
  +MISSILEEVENMORE
  +NEVERRESPAWN
  states
  {
  Spawn:
     FMNE A 1 A_Look
     loop
  See:
     FMNE A 1 A_Chase
     loop
  Missile:
     FMNE A 0 A_FaceTarget
     FMNE A 1 A_Die
     goto Death
  Death:
     FMNE A 0 A_CustomMissile("InvisiFlemBang",0)
     BAL2 CDE 5
     stop
  }
}

actor InvisiFlemBang
{
  speed 0
  obituary "%o was carelessly blown away by a landmine."
  seesound "imp/shotx"
  DamageType Flem
  Radius 1
  Height 1
  PROJECTILE
  +NOEXTREMEDEATH
  states
  {
  Spawn:
    TNT1 A 0
    goto Death
  Death:
    TNT1 A 1 A_Explode(160,200)
    stop
  }
}

actor CeilingFlemoid 5230
{
  obituary "%o was struck by flem from the sky."
  health 0x7fffffff
  painchance 0
  damagetype Flem
  speed 0
  radius 24
  height 3
  mass 50000
  damagefactor "Flem",0
  DamageFactor "Zorch",0
  DamageFactor "Melee",0
  Species "Flemoid"
  MONSTER
  +FLOORCLIP
  +NORADIUSDMG
  +SPAWNCEILING
  +NEVERRESPAWN
  +LOOKALLAROUND
  +NOGRAVITY
  +NOBLOOD
  -COUNTKILL
  states
  {
  Spawn:
    CEIL A 10 A_Look
    loop
  See:
    CEIL A 2 A_Chase
    loop
  Missile:
    CEIL BCD 8 A_FaceTarget
    CEIL E 8 A_SpawnItemEx("CeilingFlemoidBall",0,0,-11)
    CEIL FB 8
    goto See
  Death:
    CEIL E 8
    CEIL F 8
    CEIL B 8 A_NoBlocking
    stop
  }
}

actor CeilingFlemoidSource 5231
{
  hitobituary "%o was struck by flem from the sky."
  health 1000
  painchance 0
  damagetype Flem
  speed 0
  Species "Flemoid"
  radius 86
  height 56
  mass 50000
  scale 4.0
  damagefactor "Flem",0
  MONSTER
  +FLOORCLIP
  +NEVERRESPAWN
  states
  {
  Spawn:
    SORC A 10
    loop
  See:
    SORC A 2 A_Chase
    loop
  Death:
    SORC BCD 8
    stop
  }
}

actor CeilingFlemoidBall
{
  Radius 6
  Height 8
  Speed 0
  Damage 10
  Projectile
  +RANDOMIZE
  -NOGRAVITY
  RenderStyle Normal
  Alpha 1
  SeeSound "imp/attack"
  DeathSound "imp/shotx"
  States
  {
  Spawn:
    BAL1 AB 4
    loop
  Death:
    BAL1 CDE 6
    stop
  }
}

actor FlemTentacle 31412
{
   health 0x7fffffff
   Mass 50000
   radius 24
   height 64
   damagefactor "Flem",0
   DamageFactor "Zorch",0
   DamageFactor "Melee",0
   PainChance 120
   Speed 0
   Species "Flemoid"
   MONSTER
   +MISSILEMORE
   +MISSILEEVENMORE
   +LOOKALLAROUND
   -PUSHABLE
   +NOTARGET
   +THRUGHOST
   +DONTBLAST
   -CANBLAST
   +NORADIUSDMG
   -COUNTKILL
   +NOBLOOD
   +NOPAIN
   obituary "%o was gooed down by a Flemoid Tentacle."
   states
   {
   Spawn:
	TENT ABCDEFGHIJKLMNMLKJIHGFEDCB 20 A_Look
	loop
   See:
	TENT ABCDEFGHIJKLMNMLKJIHGFEDCB 5 A_Chase
	loop
   Missile:
	TENT OPQ 5
	TENT R 5 A_CustomMissile("TentacleBall")
	TENT ST 5
	goto See+6
   Death:
        TNT1 A 0
        stop
   }
}

actor TentacleBall
{
  Radius 6
  Height 8
  Speed 20
  Damage 7
  Projectile
  +RANDOMIZE
  -NOGRAVITY
  RenderStyle Normal
  Alpha 1
  Damagetype Flem
  SeeSound "imp/attack"
  DeathSound "imp/shotx"
  States
  {
  Spawn:
    BAL1 AB 4
    loop
  Death:
    BAL1 CDE 6
    stop
  }
}

ACTOR FlemoidPowerStrand 2037
{
	Health 20
	Radius 15
	Height 60
	Mass 10000
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	+ACTIVATEMCROSS
	+DONTGIB
	+NOICEDEATH
	+OLDRADIUSDMG
	+RANDOMIZE
	DeathSound "weapons/rocklx"
	Obituary "%o was hit by interdimensional slime."
	States
	{
	Spawn:
		PFLM ABCD 4 BRIGHT
		Loop
	Death:
		PFLM E 5 BRIGHT
		PFLM F 5 BRIGHT
		PFLM G 5 BRIGHT
		PFLM H 5 BRIGHT A_SpawnItem("InvisiBang3")
		PFLM I 10 BRIGHT
		PFLM I 1050 BRIGHT A_BarrelDestroy
		PFLM I 5 A_Respawn
		Wait
	}
}

ACTOR ExplodingCanister 2036
{
	Health 20
	Radius 15
	Height 60
	Mass 10000
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	+ACTIVATEMCROSS
	+DONTGIB
	+NOICEDEATH
	+OLDRADIUSDMG
	+RANDOMIZE
	DeathSound "weapons/rocklx"
	Obituary "%o was too close to an exploding canister."
	States
	{
	Spawn:
		ECAN AB 4
		Loop
	Death:
		ECAN C 5 BRIGHT
		ECAN D 5 BRIGHT
		ECAN E 5 BRIGHT
		ECAN F 5 BRIGHT A_SpawnItem("InvisiBang4")
		ECAN F 10 BRIGHT
		ECAN F 1050 BRIGHT A_BarrelDestroy
		ECAN F 5 A_Respawn
		Wait
	}
}

Actor FlemSteamSpawner : SwitchableDecoration 10003 //DoomEdNum Here
{
  Height 2
  Radius 1
  Mass 0
  Species "Flemoid"
  +DontHurtSpecies
  +NoBlockMap
  +NoGravity
  +NoInteraction
  +ClientSideOnly
  States
  {
  Spawn:
  Active:
    TNT1 A 0
    TNT1 A 0 A_JumpIf(Args[0] == 1, "Down")
    TNT1 A 0 A_JumpIf(Args[0] > 1, "Forward")
    TNT1 A 0 A_JumpIf(Args[1] > 0, "UpBurst")
    TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
    TNT1 A 0 A_PlaySoundEx("Steam/Loop", "SoundSlot7", 1)
    TNT1 A 1 A_SpawnItemEx("FlemSteamParticle", 0, 0, 1, (0.1)*Random(0, 4), 0, (0.1)*Random(35, 40), Random(0, 360), 128)
    Loop
  Down:
    TNT1 A 0 A_JumpIf(Args[1] > 0, "DownBurst")
    TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
    TNT1 A 0 A_PlaySoundEx("Steam/Loop", "SoundSlot7", 1)
    TNT1 A 1 A_SpawnItemEx("FlemSteamParticle", 0, 0, 0, (0.1)*Random(0, 4), 0, (-0.1)*Random(35, 40), Random(0, 360), 128)
    Loop
  Forward:
    TNT1 A 0 A_JumpIf(Args[1] > 0, "ForwardBurst")
    TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
    TNT1 A 0 A_PlaySoundEx("Steam/Loop", "SoundSlot7", 1)
    TNT1 A 1 A_SpawnItemEx("FlemSteamParticle", 0, 0, 0, (0.1)*Random(35, 40), 0, (0.1)*Random(-5, 5), Random(-8, 8), 128)
    Loop
  UpBurst:
    TNT1 A 1 A_Jump(Args[2]/16, 1)
    Loop
    TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
    TNT1 A 0 A_PlaySoundEx("Steam/Fire", "SoundSlot7", 0)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("FlemSteamParticle", 0, 0, 1, (0.1)*Random(0, 4), 0, (0.1)*Random(35, 40), Random(0, 360), 128)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("FlemSteamParticle", 0, 0, 1, (0.1)*Random(0, 4), 0, (0.1)*Random(35, 40), Random(0, 360), 128)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("FlemSteamParticle", 0, 0, 1, (0.1)*Random(0, 4), 0, (0.1)*Random(35, 40), Random(0, 360), 128)
    Loop
  DownBurst:
    TNT1 A 1 A_Jump(Args[2]/16, 1)
    Loop
    TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
    TNT1 A 0 A_PlaySoundEx("Steam/Fire", "SoundSlot7", 0)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("FlemSteamParticle", 0, 0, 0, (0.1)*Random(0, 4), 0, (-0.1)*Random(35, 40), Random(0, 360), 128)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("FlemSteamParticle", 0, 0, 0, (0.1)*Random(0, 4), 0, (-0.1)*Random(35, 40), Random(0, 360), 128)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("FlemSteamParticle", 0, 0, 0, (0.1)*Random(0, 4), 0, (-0.1)*Random(35, 40), Random(0, 360), 128)
    Loop
  ForwardBurst:
    TNT1 A 1 A_Jump(Args[2]/16, 1)
    Loop
    TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
    TNT1 A 0 A_PlaySoundEx("Steam/Fire", "SoundSlot7", 0)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("FlemSteamParticle", 0, 0, 0, (0.1)*Random(35, 40), 0, (0.1)*Random(-5, 5), Random(-8, 8), 128)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("FlemSteamParticle", 0, 0, 0, (0.1)*Random(35, 40), 0, (0.1)*Random(-5, 5), Random(-8, 8), 128)
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx("FlemSteamParticle", 0, 0, 0, (0.1)*Random(35, 40), 0, (0.1)*Random(-5, 5), Random(-8, 8), 128)
    Loop
  Inactive:
    TNT1 A 1 A_StopSoundEx("SoundSlot7")
    TNT1 A 1
    Goto Inactive+1
  }
}

Actor FlemSteamParticle
{
  Height 1
  Radius 1
  Damage 1
  Damagetype "Flem"
  +Missile
  +NoGravity
  +NoBlockMap
  RenderStyle Add
  Scale 0.4
  Alpha 0.65
  translation "80:111=112:127"
  States
  {
  Spawn:
    STEM A 2 A_SetTranslucent(0.6, 1)
    STEM B 2 A_SetTranslucent(0.55, 1)
    STEM C 2 A_SetTranslucent(0.5, 1)
    STEM D 2 A_SetTranslucent(0.45, 1)
    STEM E 2 A_SetTranslucent(0.4, 1)
    STEM F 2 A_SetTranslucent(0.35, 1)
    STEM G 2 A_SetTranslucent(0.3, 1)
    STEM H 2 A_SetTranslucent(0.25, 1)
    STEM I 2 A_SetTranslucent(0.2, 1)
    STEM J 2 A_SetTranslucent(0.15, 1)
    STEM K 2 A_SetTranslucent(0.1, 1)
    STEM L 2 A_SetTranslucent(0.05, 1)
    Stop
  }
}
